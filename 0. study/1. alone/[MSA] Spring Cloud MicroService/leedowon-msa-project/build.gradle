import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.7.22'
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
        maven { url 'https://artifactory-oss.prod.netflix.net/artifactory/maven-oss-candidates' }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = JavaVersion.VERSION_17.toString()
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'

    dependencies {
        // Kotlin
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

        // Spring Web Based On Tomcat
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // Spring Boot Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // Service Registry Client
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // Devtools
        developmentOnly 'org.springframework.boot:spring-boot-devtools'

        // Spring Web Validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // H2
        runtimeOnly "com.h2database:h2"

        // Model Mapper
        implementation 'org.modelmapper:modelmapper:3.1.1'

        // Spring Data Jpa
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.0.6'

        // kotlin logging
        implementation 'io.github.microutils:kotlin-logging:3.0.5'
    }

    ext {
        set('springCloudVersion', "2022.0.1")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
